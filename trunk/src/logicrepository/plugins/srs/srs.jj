options {
  STATIC = false;
  ERROR_REPORTING = true;
}

PARSER_BEGIN(SRSParser)
  package logicrepository.plugins.srs;

  import java.util.ArrayList;
  import java.util.HashMap;
  import java.util.HashSet;
  import java.io.*;

  public class SRSParser {
    public static SRSParser parse(InputStream input) {
      SRSParser srsParser = new SRSParser(input);
      try{
        srsParser.Start();
      }
      catch(Exception e){
        System.err.println(e.getMessage());
        System.exit(1);
      }
      return srsParser;
    }

    public static void main(String[] args) throws ParseException, TokenMgrError {
      SRSParser parser = new SRSParser(System.in);
      parser.Start();
      SRS srs = parser.getSRS();
      System.out.println(srs.toString());
    }
  
    private SRS srs;
    public SRS getSRS() { return srs; }

  }

PARSER_END(SRSParser)

SKIP : {
  <WHITESPACE: [" ","\t", "\r", "\n"]>
}

TOKEN : {
    <EPSILON   : "\\epsilon" >
  | <PERIOD    : "."       >
  | <ARROW     : "->"      > 
  | <TERMINAL  : ["A"-"z"](["A"-"z"]|"_"|["0"-"9"])* >
}

void Start() : {
  srs = new SRS();
}
{
  Srs() <EOF>
}

void Srs() : {
  Rule r;
}
{
  r = Rule()
  { srs.add(r); }
  "."
  (
   r = Rule()
   {srs.add(r); }
  "." )* 
}

Rule Rule() : {
  Sequence lhs;
  Sequence rhs;
  Sequence s;
}
{
  s = Sequence()
  {lhs = s; } 
  "->"
  (s = Sequence() | s = Epsilon())
  {rhs = s; }
  { return new Rule(lhs,rhs); }
}

Sequence Sequence() : {
  Sequence ret = new Sequence();
  Symbol s;
  Token t;
}
{
  t = <TERMINAL>
  { ret.add(Symbol.get(t.image)); }
  (
    t = <TERMINAL>
    { ret.add(Symbol.get(t.image)); }
  )*
  { return ret; }
  
}

Sequence Epsilon() : {}
{
  <EPSILON>
  { return new Sequence(); }
}
